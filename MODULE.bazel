module(name = "com_github_buildbarn_bb_deployments")

###############################################################################
# Buildbarn Services
###############################################################################
bazel_dep(name = "com_github_buildbarn_bb_remote_execution")
git_override(
    module_name = "com_github_buildbarn_bb_remote_execution",
    commit = "babfe8f73772adb40306ee23546d3f827d120998",
    remote = "https://github.com/buildbarn/bb-remote-execution.git",
)

bazel_dep(name = "com_github_buildbarn_bb_storage")
git_override(
    module_name = "com_github_buildbarn_bb_storage",
    commit = "51e1a67922e22d65445f43aa809636cb481644f3",
    remote = "https://github.com/buildbarn/bb-storage.git",
)


# required to be set by bb-remote-execution
bazel_dep(name = "com_github_buildbarn_go_xdr")
git_override(
    module_name = "com_github_buildbarn_go_xdr",
    commit = "236788cf9e8948a1e8875c7dd243e2be180cdfd7",
    remote = "https://github.com/buildbarn/go-xdr.git",
)

###############################################################################
# Required toolchain to build the services
###############################################################################
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "toolchains_cc")
git_override(
    module_name = "toolchains_cc",
    commit = "f4eee86cfe43d10c78cc6be9b61720ac59bacecc",
    remote = "https://github.com/reutermj/toolchains_cc.git",
)
register_toolchains(
    "@toolchains_cc",
    dev_dependency = True,
)

###############################################################################
# Misc Required dependency
###############################################################################
# required to be set by bb-remote-execution
bazel_dep(name = "rules_antlr")
git_override(
    module_name = "rules_antlr",
    commit = "89a29cca479363a5aee53e203719510bdc6be6ff",
    patches = [
        "//:patches/rules_antlr/antlr-4.10.diff",
        "//:patches/rules_antlr/bzlmod.diff",
    ],
    remote = "https://github.com/marcohu/rules_antlr.git",
)

# required to be set by bb-remote-execution and bb-storage
bazel_dep(name = "bazel_remote_apis", version = "0.0.0")
git_override(
    module_name = "bazel_remote_apis",
    commit = "3051389c06348307437e92e3a1d3c6d6566094b4",
    remote = "https://github.com/bazelbuild/remote-apis.git",
)

###############################################################################
# Not required, maybe nice to have linting
###############################################################################
bazel_dep(name = "buildifier_prebuilt", version = "6.4.0", dev_dependency = True)

###############################################################################
# I think this is just for testing the bare deployment
###############################################################################
bazel_dep(name = "abseil-cpp", version = "20250127.0")
bazel_dep(name = "googleapis", version = "0.0.0-20250703-f9d6fe4a")
bazel_dep(name = "googletest", version = "1.16.0")
http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
http_archive(
    name = "abseil-hello",
    patch_cmds = [
        "sed -i 's/com_google_absl/abseil-cpp/' BUILD.bazel",
        "sed -i 's/com_google_googletest/googletest/' BUILD.bazel",
    ],
    sha256 = "e676640e69e210636de795f571237bec09a9ad9af6e441bf56f0d193cfe1c9fc",
    strip_prefix = "abseil-hello-b4803b41ab3d58c503265148e5a7d3fd2a8e46d3/bazel-hello",
    urls = ["https://github.com/abseil/abseil-hello/archive/b4803b41ab3d58c503265148e5a7d3fd2a8e46d3.zip"],
)

###############################################################################
# Python for the bare deployment launcher
###############################################################################
bazel_dep(name = "rules_python", version = "1.7.0")
bazel_dep(name = "aspect_rules_py", version = "1.6.6")
bazel_dep(name = "rules_uv", version = "0.89.2")

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    is_default = True,
    python_version = "3.11",
)

###############################################################################
# Go dependencies required by Buildbarn services (bb-storage, bb-remote-execution)
# These are needed to build the upstream Go binaries, not for our deployment code.
###############################################################################
bazel_dep(name = "rules_go", version = "0.56.1")

bazel_dep(name = "gazelle", version = "0.45.0")
single_version_override(
    module_name = "gazelle",
    patches = ["//:patches/gazelle/dont-flatten-srcs.diff"],
)

go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.24.5")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")

go_deps_dev = use_extension("@gazelle//:extensions.bzl", "go_deps", dev_dependency = True)

# NB 2024-11-18: Solve issues with BUILD file paths in xds.
# https://github.com/cncf/xds/issues/104
go_deps_dev.gazelle_override(
    build_file_generation = "on",
    path = "github.com/cncf/xds/go",
)
go_deps_dev.gazelle_override(
    build_file_generation = "on",
    directives = [
        "gazelle:go_grpc_compilers @io_bazel_rules_go//proto:go_proto,@io_bazel_rules_go//proto:go_grpc_v2",
        "gazelle:resolve proto go build/bazel/semver/semver.proto //build/bazel/semver",
        "gazelle:resolve proto go google/api/annotations.proto @org_golang_google_genproto_googleapis_api//annotations",
        "gazelle:resolve proto go google/longrunning/operations.proto @com_google_cloud_go_longrunning//autogen/longrunningpb",
        "gazelle:resolve proto go google/rpc/status.proto @org_golang_google_genproto_googleapis_rpc//status",
        "gazelle:resolve proto google/api/annotations.proto @googleapis//google/api:annotations_proto",
        "gazelle:resolve proto google/longrunning/operations.proto @googleapis//google/longrunning:operations_proto",
        "gazelle:resolve proto google/rpc/status.proto @googleapis//google/rpc:status_proto",
    ],
    path = "github.com/bazelbuild/remote-apis",
)
go_deps_dev.module_override(
    patches = ["@com_github_buildbarn_bb_storage//:patches/org_golang_google_genproto_googleapis_bytestream/service-registrar.diff"],
    path = "google.golang.org/genproto/googleapis/bytestream",
)
go_deps_dev.module_override(
    patches = ["//:patches/org_golang_x_sys/o-search.diff"],
    path = "golang.org/x/sys",
)
go_deps_dev.module_override(
    patches = [
        "@com_github_buildbarn_bb_remote_execution//:patches/com_github_hanwen_go_fuse_v2/direntrylist-offsets-and-testability.diff",
        "@com_github_buildbarn_bb_remote_execution//:patches/com_github_hanwen_go_fuse_v2/writeback-cache.diff",
        "@com_github_buildbarn_bb_remote_execution//:patches/com_github_hanwen_go_fuse_v2/notify-testability.diff",
    ],
    path = "github.com/hanwen/go-fuse/v2",
)
